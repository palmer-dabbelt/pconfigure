LANGUAGES   += c
COMPILEOPTS += -Wall
COMPILEOPTS += -Wextra
COMPILEOPTS += -Werror
COMPILEOPTS += -Wno-unused-parameter
COMPILEOPTS += -ansi
COMPILEOPTS += -pedantic

LANGUAGES   += bash

LANGUAGES   += h

LANGUAGES   += pkgconfig
LINKOPTS    += -Sobj/proc/version-pc.sed

LANGUAGES   += c++
COMPILEOPTS += -Wall
COMPILEOPTS += -Wextra
COMPILEOPTS += -Werror
COMPILEOPTS += -std=c++11

# Allows me to staticly link my internal C++ library dependencies --
# note that this isn't just an optimization, it's actually necessary
# because of how I handle C++ header files.
LANGUAGES   += c++
COMPILEOPTS += -Isrc

GENERATE    += version.h
GENERATE    += version-pc.sed

##############################################################################
# Libraries                                                                  #
##############################################################################
HEADERS     += pinclude.h
SOURCES     += libpinclude/pinclude.h

HEADERS     += pinclude.h++
SOURCES     += libpinclude/pinclude.h++

LIBRARIES   += libpinclude.so
SOURCES     += libpinclude/library.c

LIBRARIES   += pkgconfig/pinclude.pc
LINKOPTS    += `ppkg-config --optional --have TALLOC talloc --pkgconfig`
SOURCES     += libpinclude/pinclude.pc

##############################################################################
# Binaries                                                                   #
#############################################################################
LIBEXECS    += pconfigure-legacy
LINKOPTS    += `ppkg-config  --optional --have TALLOC talloc --libs`
COMPILEOPTS += `ppkg-config  --optional --have TALLOC talloc --cflags`
COMPILEOPTS += -Isrc/extern
DEPLIBS     += pinclude
SOURCES     += pconfigure/main.c
TESTSRC     += version.bash
TESTSRC     += simple-c.bash
TESTSRC     += tek.bash
TESTSRC     += bash-deps.bash
TESTSRC     += c-cxx.bash
TESTSRC     += c-ifndef.bash
TESTSRC     += syshdr.bash
TESTSRC     += autodeps.bash
TESTSRC     += osx-shared-deps.bash
TESTSRC     += man.bash
TESTSRC     += subdir-rpath.bash

BINARIES    += pclean
SOURCES	    += pclean.bash
TESTSRC     += simple.bash

BINARIES    += pbashc
DEPLIBS     += pinclude
COMPILEOPTS += -DLANG_BASH
SOURCES	    += pbashc.c
TESTSRC     += simple.bash
TESTSRC     += samedir.bash
TESTSRC     += define.bash
TESTSRC     += define2.bash

BINARIES    += pperlc
DEPLIBS     += pinclude
COMPILEOPTS += -DLANG_PERL
SOURCES	    += pbashc.c
TESTSRC     += simple.bash
TESTSRC     += samedir.bash

BINARIES    += ppkg-config
SOURCES	    += ppkg-config.bash

BINARIES    += pgcc-config
SOURCES     += pgcc-config.bash

BINARIES    += pscalac
SOURCES	    += pscala/compiler.bash

BINARIES    += pscalald
SOURCES	    += pscala/linker.bash

BINARIES    += pnasm
SOURCES	    += pnasm.bash

BINARIES    += ptest
SOURCES	    += ptest.bash

BINARIES    += pinclude
DEPLIBS     += pinclude
DEPLIBS     += pinclude++
SOURCES     += pinclude.c++
TESTSRC     += empty.bash
TESTSRC     += nonexist.bash
TESTSRC     += samedir.bash
TESTSRC     += samedir2.bash
TESTSRC     += indir.bash
TESTSRC     += existafter.bash
TESTSRC     += comment.bash

BINARIES    += pllvm-config
SOURCES     += pllvm-config.bash

BINARIES    += pwinegcc
SOURCES	    += pwinegcc.bash

##############################################################################
# Rewrite-Related Libraries                                                  #
##############################################################################
# A C++ frontend to pinclude, which has a bunch of nasty C-isms in it.
LIBRARIES   += libpinclude++.so
SOURCES     += libpinclude/library.c++

LIBRARIES   += pkgconfig/pinclude++.pc
SOURCES     += libpinclude/pinclude++.pc

# pconfigure as a library, which can be used by other build systems that aren't
# quite pconfigure.
LIBRARIES   += libpconfigure.so
DEPLIBS     += pinclude
DEPLIBS     += pinclude++
CONFIG      += libpconfigure_sources
CONFIG      += libpconfigure_headers

LIBRARIES   += pkgconfig/pconfigure.pc
SOURCES     += libpconfigure/pconfigure.pc

LIBRARIES   += libmakefile.so
CONFIG      += libmakefile_sources

##############################################################################
# Rewrite-Related Binaries                                                   #
##############################################################################
# A rewrite of pconfigure that's designed to bring the whole system
# into the 21st century!
BINARIES    += pconfigure
AUTODEPS     = false
DEPLIBS     += pinclude++
DEPLIBS     += pconfigure
DEPLIBS     += makefile
SOURCES     += pconfigure++.c++
TESTSRC     += version.bash
TESTSRC     += simple-c.bash
TESTSRC     += tek.bash
TESTSRC     += bash-deps.bash
TESTSRC     += c-cxx.bash
TESTSRC     += c-ifndef.bash
TESTSRC     += syshdr.bash
TESTSRC     += autodeps.bash
TESTSRC     += osx-shared-deps.bash
TESTSRC     += man.bash
TESTSRC     += subdir-rpath.bash
TESTSRC     += config.bash
TESTSRC     += compileopts-in-language.bash
TESTSRC     += language-after-binary.bash
TESTSRC     += bash-test-opts.bash
TESTSRC     += h.bash
TESTSRC     += exec-config.bash
TESTSRC     += prefix.bash
TESTSRC     += gitdate.bash
TESTSRC     += implicit_h.bash
TESTSRC     += c-test.bash
TESTSRC     += c-deplib-test.bash
TESTSRC     += multi-language-build.bash
TESTSRC     += execute-line.bash
TESTSRC     += multi-static-link.bash
TESTSRC     += dashdash-config.bash
TESTSRC     += srcpath.bash
TESTSRC     += palmer-scripts.bash
TESTSRC     += headersrc.bash
TESTSRC     += putil.bash
TESTSRC     += cxx14.bash
TESTSRC     += swig.bash
TESTSRC     += libdir.bash
TESTSRC     += srcdir.bash

# These command-line tools are technically availiable for external
# usage, but they're really just for internal tests.
LIBEXECS    += pconfigure/string_utils-clean_white
COMPILEOPTS += -DTEST_CLEAN_WHITE
SOURCES     += libpconfigure/string_utils.c++
TESTSRC     += no_spaces.bash
TESTSRC     += internal_space.bash
TESTSRC     += internal_spaces.bash
TESTSRC     += leading_spaces.bash
TESTSRC     += trailing_spaces.bash
TESTSRC     += tab.bash

LIBEXECS    += pconfigure/string_utils-strip_char
COMPILEOPTS += -DTEST_STRIP_CHAR
SOURCES     += libpconfigure/string_utils.c++
TESTSRC     += three_parts.bash

LIBEXECS    += pconfigure/vector_util-map
COMPILEOPTS += -DTEST_MAP
SOURCES     += libpconfigure/vector_util.c++
TESTSRC     += builtin.bash

BINARIES    += phc
SOURCES     += phc.bash

BINARIES    += pswigcc
SOURCES     += pswigcc.bash
